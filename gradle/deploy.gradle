apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version '0.1.4'
group 'com.github.takahirom.downloadable.calligraphy'
def isDryRun = false

def gitHubUrl = 'https://github.com/takahirom/DownloadableCalligraphy.git'
def siteUrl = 'https://github.com/takahirom/DownloadableCalligraphy'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/downloadable-calligraphy-release.aar")
            groupId
            artifactId 'downloadable-calligraphy'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintrayUser') ?: project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = properties.getProperty('bintrayApiKey') ?: project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'downloadable-calligraphy'
        description = "This android library provides a way to set default (downloadable) fonts using Calligraphy methods."
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = gitHubUrl
        dryRun = isDryRun
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Downlodable Font'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'takahirom'
                        name 'takahirom'
                        email 'takam.dev@gmail.com'
                    }
                }
                scm {
                    connection gitHubUrl
                    developerConnection gitHubUrl
                    url siteUrl

                }
            }
        }
    }
}
